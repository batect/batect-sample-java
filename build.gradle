import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'com.github.ben-manes.versions' version '0.29.0'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

apply plugin: 'java'
apply plugin: 'application'

repositories {
    jcenter()
}

dependencies {
    implementation 'com.sparkjava:spark-core:2.9.2'
    implementation 'org.slf4j:slf4j-simple:1.7.30'
    implementation 'org.postgresql:postgresql:42.2.15'
    implementation 'com.squareup.okhttp3:okhttp:4.8.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'net.dongliu:gson-java8-datatype:1.1.0'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'
    testImplementation 'org.mockito:mockito-core:3.5.5'
    testImplementation 'com.jayway.jsonpath:json-path-assert:2.4.0'
}

sourceSets {
    integrationTest {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }

    journeyTest
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime

    journeyTestImplementation.extendsFrom testImplementation
    journeyTestRuntime.extendsFrom testRuntime
}

// See http://stackoverflow.com/a/36199263/1668119 for explanation
tasks.withType(Test) {
    testLogging {
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_ERROR,
                TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showCauses true
        showExceptions true
        showStackTraces true
    }
}

task integrationTest(type: Test) {
    description 'Runs the integration tests.'
    group 'Verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task journeyTest(type: Test) {
    description 'Runs the journey tests.'
    group 'Verification'
    testClassesDirs = sourceSets.journeyTest.output.classesDirs
    classpath = sourceSets.journeyTest.runtimeClasspath
}

mainClassName = 'com.charleskorn.banking.internationaltransfers.Application'

shadowJar {
    destinationDirectory = file('.batect/international-transfers-service')
    archiveClassifier = ''
    archiveVersion = ''
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
